apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-platform-android'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext.versionMajor = 2
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = true  //only true while in development
ext.minimumSdkVersion = 17


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.job.whatsappstories"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode generateVersionCode() // 170200000
        versionName generateVersionName() // 2.0.0
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }


    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libs
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:exifinterface:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:gridlayout-v7:$support_version"

    // Kotlin libs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.anko:anko-common:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"

    // Images libs
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.android.support:support-v4:28.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    // Iconics libs
    implementation 'com.mikepenz:iconics-core:3.0.3@aar'
    implementation 'com.mikepenz:iconics-views:3.0.3@aar'
    implementation 'com.mikepenz:ionicons-typeface:2.0.1.4@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar'
    implementation 'com.mikepenz:actionitembadge:3.3.2@aar'

    // Views libs
    implementation 'com.github.captain-miao:optroundcardview:1.0.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // Other libs
    implementation 'com.github.kizitonwose.time:time-android:1.0.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Constraining layout
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Permissions
    implementation 'com.karumi:dexter:4.2.0'

    // Video
    implementation 'cn.jzvd:jiaozivideoplayer:6.2.12'

    // shimmer
    implementation 'com.github.sharish:ShimmerRecyclerView:v1.3'

    //sliding root nav
    implementation 'com.yarolegovich:sliding-root-nav:1.1.0'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-ads:17.1.2'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    implementation 'com.google.firebase:firebase-dynamic-links:16.1.5'
    implementation 'com.google.firebase:firebase-inappmessaging-display:17.0.4'
}

kapt {
    generateStubs = true
    mapDiagnosticLocations = true
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
