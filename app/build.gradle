plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'idea'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-parcelize'
}

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext.versionMajor = 4
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = false  //only true while in development
ext.minimumSdkVersion = 23


android {
    compileSdk 34
    buildToolsVersion = "34.0.0"
    namespace "com.job.whatsappstories"

    defaultConfig {
        applicationId "com.job.whatsappstories"

        minSdkVersion 23
        targetSdkVersion 34
        versionCode generateVersionCode() // 170302000
        versionName generateVersionName() // 3.0.5
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }


    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            resValue("bool", "FIREBASE_CRASH_ENABLED", "false")
        }

        release {
            resValue("bool", "FIREBASE_CRASH_ENABLED", "true")
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/LICENSE'
    }

    dexOptions {
        //may speed up incremental builds
        preDexLibraries true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        javaParameters = true
    }
}


dependencies {
    def glide_version = "4.9.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libs
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    // Kotlin libs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    /*implementation "org.jetbrains.anko:anko-common:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"*/

    // Images libs
    implementation 'com.github.bumptech.glide:glide:' + glide_version
    annotationProcessor 'com.github.bumptech.glide:compiler:' + glide_version
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    // Iconics libs
    implementation 'com.mikepenz:iconics-core:3.0.3@aar'
    implementation 'com.mikepenz:iconics-views:3.0.3@aar'
    implementation 'com.mikepenz:ionicons-typeface:2.0.1.4@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar'
    implementation 'com.mikepenz:actionitembadge:3.3.2@aar'

    // Views libs
    implementation 'com.makeramen:roundedimageview:2.3.0'

    //play update core
    implementation("com.google.android.play:app-update-ktx:2.1.0")

    // Other libs
    implementation 'com.github.kizitonwose.time:time-android:1.0.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Constraining layout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Permissions
    implementation 'com.karumi:dexter:5.0.0'

    // Video
    implementation 'cn.jzvd:jiaozivideoplayer:6.2.12'

    // shimmer
    implementation 'com.github.sharish:ShimmerRecyclerView:v1.3'

    //sliding root nav
    implementation 'com.github.JobGetabu:SlidingRootNav:v2.0.2'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    //ads
    implementation project(':nativetemplates')

    //firebase
    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'

    implementation 'com.google.firebase:firebase-inappmessaging-ktx'
    implementation 'com.google.firebase:firebase-inappmessaging-display-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    implementation 'com.google.firebase:firebase-ads:19.0.1'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'
}

kapt {
    generateStubs = true
    mapDiagnosticLocations = true
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
